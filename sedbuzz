#sedbuzz is fizzbuzz in sed
#uses extended regular expressions

#quit on q, ignore other non-integers
/^q$/q
/[0-9]/!d

#check divisible by 5
/[05]$/s/(.*)/&buzz/

/^[0-9]+(buzz)?/ {
#check divisible by 3 
    s/([0-9]*)+(buzz)?/\1\
\1\2/g

    #convert to unary
    :LOOP
    s/0(0*)(buzz|$)/_\1\2/
    s/1($|buzz|_)/0\1/g
    s/2($|buzz|_)/1\1/g
    s/3($|buzz|_)/2\1/g
    s/4($|buzz|_)/3\1/g
    s/5($|buzz|_)/4\1/g
    s/6($|buzz|_)/5\1/g
    s/7($|buzz|_)/6\1/g
    s/8($|buzz|_)/7\1/g
    s/9($|buzz|_)/8\1/g
    s/_/9/
    s/([0-9]*\n|^)_(buzz|$)/\10\2/
    x
    s/(.*)/\11/
    x
    /(\n|^)00*(b|$)/!b LOOP

    #check divisibility
    x
    s/1{3}//g
    /^$/s/^/fizz/
    /fizz/G
    /fizz/!x
    /fizz|buzz/s/\n//g

#print numbers
    /fizz|buzz/!P

#print fizz/buzz/fizzbuzz
    s/[0-9]//g
    /fizz|buzz/p

#zero out hold
    s/.*//
    x
}
