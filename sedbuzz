#!/bin/sed -nrf
#sedbuzz is fizzbuzz in sed

#quit on q, ignore other non-integers
/^q$/q
/^[0-9]+$/!d

/^[0-9]+/ {

    #preserve original value
    h

    #check divisibility by 3 via casting out threes.
    s/[47]/1/g
    s/[58]/2/g
    s/[0369]//g
    :CAST_OUT
    s/11/2/g
    s/22/1/g
    s/12//g
    s/21//g
    /^[12]?$/ !b CAST_OUT

    /^$/ {
	s/^$/fizz/
	H
    }
    x

    #check divisibility by 5
    /(5|[0-9]+0)(\nfizz)?$/s/(.*)/&buzz/
    #format and print result
    /fizz|buzz/ {
        s/[0-9]*\n?//g
    }
    P

    #clear hold buffer
    s/.*//
    x
}
